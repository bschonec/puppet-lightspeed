# @summary A short summary of the purpose of this class
#
# A description of what this class does
#
# @example
#   include lightspeed
#
# @param package_name
#   The name of the package to install.
#
# @param package_state
#   The installation state of $package_name.
#
# @param service_name
#   The name of the Lightspeed service daemon.
#
# @param service_enable
#   The state of the Lightspeed service clad.
#
# @param service_ensure
#   The state of the Lightspeed service clad.
#
# @param backend_endpoint
#   The URL to the AI Endpoint Server.
#
# @param logging_level
#   The logging level.
#
# @param history_enabled
#   If the history for all conversation should be enabled or not.
#
# @param backend_auth_cert_file
#   The path to the certificate file generated by RHSM.
#
# @param backend_auth_key_file
#   The path to the key file generated by RHSM.
#
# @param backend_auth_verify_ssl
#   If SSL verification should be set. Don't disabled this for production server.
#
# @param logging_audit_enabled
#   If enabled, we will log audit tracing to journald.
#
# @param database_type
#   Enum of database type, 'sqlite', 'postgresql' or 'mysql'.
#
# @param database_connection_string
#   Connection string refers to the path where the sqlite database will be placed. Only available with sqlite type.
#
class lightspeed (
  Stdlib::HTTPUrl $backend_endpoint                     = 'https://satellite.redhat.com/api/lightspeed/v1',
  Enum['sqlite', 'postgresql', 'mysql'] $database_type = 'sqlite',
  String[1] $database_connection_string                 = '/var/lib/command-line-assistant/history.db',
  String[1] $package_name                               = 'command-line-assistant',
  String[1] $service_name                               = 'clad',
  Boolean $service_enable                               = true,
  Boolean $backend_auth_verify_ssl                      = true,
  Boolean $logging_audit_enabled                        = false,
  Boolean $history_enabled                              = true,
  Stdlib::Ensure::Service $service_ensure               = 'running',
  Enum['installed', 'present', 'absent'] $package_state = 'installed',
  Enum['INFO', 'DEBUG'] $logging_level                  = 'INFO',
  Stdlib::Absolutepath $backend_auth_cert_file          = '/etc/pki/consumer/cert.pem',
  Stdlib::Absolutepath $backend_auth_key_file           = '/etc/pki/consumer/key.pem'
) {
  $config_file_name = '/etc/xdg/command-line-assistant/config.toml'

  package { $package_name:
    ensure => $package_state,
  }

  file { $config_file_name:
    ensure  => file,
    owner   => root,
    group   => root,
    mode    => '0600',
    content => template('lightspeed/config.toml.erb'),
    require => Package[$package_name],
  }

  service { $service_name:
    ensure  => $service_ensure,
    enable  => $service_enable,
    require => File[$config_file_name],
  }
}
